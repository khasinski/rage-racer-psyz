#version 450

layout(location = 0) in vec4 vertexColor;
layout(location = 1) in vec2 rawUV;
layout(location = 2) flat in uint tpage;
layout(location = 3) flat in uint clut;
layout(location = 4) flat in uint textureMode;
layout(location = 5) flat in uint subPixelMask;
layout(location = 6) flat in uint texelShift;
layout(location = 7) flat in uint indexShift;
layout(location = 8) flat in uint indexMask;
layout(location = 9) flat in uint dither;
layout(location = 10) flat in ivec2 pageBase;
layout(location = 11) flat in uvec4 texWindow;

layout(set = 2, binding = 0) uniform sampler2D texVram;

uvec2 resolveTexel() {
    // The PS1 rasterizer consumes the integer part of its fixed-point UV
    // accumulator. Rounding to nearest moves coherent areas onto adjacent
    // palette indices (often transparent/black index zero).
    // Stabilize values which are mathematically integral but arrive a few
    // float ULPs below the boundary after hardware interpolation. Half of one
    // 16.16 accumulator unit cannot advance a distinct PS1 fixed-point value.
    const vec2 fixedHalfUnit = vec2(1.0 / 131072.0);
    uvec2 texel = uvec2(clamp(floor(rawUV + fixedHalfUnit),
                              vec2(0.0), vec2(255.0)));
    return (texel & texWindow.xy) | texWindow.zw;
}

layout(location = 0) out vec4 FragColor;

uint rgb5551ToU16(vec4 c) {
    uint r = uint(c.r * 31.0 + 0.5);
    uint g = uint(c.g * 31.0 + 0.5);
    uint b = uint(c.b * 31.0 + 0.5);
    uint a = uint(c.a + 0.5);
    return r | (g << 5u) | (b << 10u) | (a << 15u);
}

const mat4 ditherMatrix = mat4(
    -4.0, +0.0, -3.0, +1.0,
    +2.0, -2.0, +3.0, -1.0,
    -3.0, +1.0, -4.0, +0.0,
    +3.0, -1.0, +2.0, -2.0);
vec3 applyDither(vec3 c) {
    if (dither == 0u) return c;
    int dx = int(gl_FragCoord.x) & 3;
    int dy = int(gl_FragCoord.y) & 3;
    float off = ditherMatrix[dx][dy];
    vec3 c8 = c * 255.0 + off;
    vec3 c5 = clamp(floor(c8 / 8.0), 0.0, 31.0);
    return c5 / 31.0;
}

void main() {
    vec4 texColor;
    if (textureMode == 0u) { // untextured
        texColor = vec4(1, 1, 1, 2);
    } else if (textureMode == 1u) { // 16-bit bitmap
        texColor = texelFetch(texVram, pageBase + ivec2(resolveTexel()), 0);
    } else { // indexed
        ivec2 texel = ivec2(resolveTexel());
        uint subPixel = uint(texel.x) & subPixelMask;
        ivec2 texelPos = ivec2(texel.x >> texelShift, texel.y);
        uint word16 = rgb5551ToU16(texelFetch(texVram, pageBase + texelPos, 0));
        uint colorIdx = (word16 >> (subPixel * indexShift)) & indexMask;
        ivec2 clutBase = ivec2((clut % 64u) * 16u, clut / 64u);
        texColor = texelFetch(texVram, clutBase + ivec2(colorIdx, 0), 0);
    }
    // check for full transparency
    if (texColor == vec4(0, 0, 0, 0)) {
        // PS1 texel 0000h is a color key: it leaves the framebuffer untouched,
        // even when the primitive itself is not using semi-transparency.
        discard;
    }
    // check for setSemiTrans(p, 1)
    bool isSemiTrans = vertexColor.a < 0.75;
    // when a color has the 0x8000 bit left then it has the semitrans flag on
    bool colorSemiTrans = texColor.a > 0;
    // PS1-accurate texture-color modulation: (tex5 * col8) >> 7, clamp to 31
    vec3 modColor;
    if (textureMode == 0u) {
        // untextured: PS1 uses color directly, no modulation
        modColor = texColor.rgb * vertexColor.rgb;
    } else {
        vec3 tex5 = floor(texColor.rgb * 31.0 + 0.5);
        vec3 col8 = min(floor(vertexColor.rgb * 127.5 + 0.5), vec3(255.0));
        vec3 prod8 = min(tex5 * col8 / 16.0, vec3(255.0));
        modColor = dither != 0u ? prod8 / 255.0
                                : floor(prod8 / 8.0) / 31.0;
    }
    modColor = applyDither(modColor);
    // pre-multiplied alpha output for ONE, ONE_MINUS_SRC_ALPHA blending
    if (colorSemiTrans && isSemiTrans) {
        uint abr = (tpage & 0x60u) >> 5u;
        if (abr == 0u) {
            FragColor = vec4(modColor * 0.5, 0.5); // 50% blend
        } else if (abr == 1u) {
            FragColor = vec4(modColor, 0.0); // additive
        } else if (abr == 2u) {
            FragColor = vec4(modColor, 0.0); // subtractive
        } else {                                  // abr == 3u
            FragColor = vec4(modColor * 0.25, 0.0); // B + F/4
        }
    } else {
        FragColor = vec4(modColor, 1.0); // full opacity
    }
}
